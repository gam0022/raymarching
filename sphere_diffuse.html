<html><head><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">
precision highp float;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

float distSphere(vec3 p, float r) {
  return length(p) - r;
}

// distance function
float distScene(vec3 p){
  return distSphere(p, 1.3);
}

vec3 getNormal(vec3 p){
  float d = 0.0001;
  return normalize(vec3(
    distScene(p + vec3(  d, 0.0, 0.0)) - distScene(p + vec3( -d, 0.0, 0.0)),
    distScene(p + vec3(0.0,   d, 0.0)) - distScene(p + vec3(0.0,  -d, 0.0)),
    distScene(p + vec3(0.0, 0.0,   d)) - distScene(p + vec3(0.0, 0.0,  -d))
  ));
}

void main(void){
  // fragment position
  vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

  // camera and ray
  vec3 cPos = vec3(0.0, 0.0, -2.0);
  vec3 cDir = normalize(vec3(0, 0, 1));
  vec3 cUp  = normalize(vec3(0, 1, 0));
  vec3 cSide = cross(cDir, cUp);
  float targetDepth = 1.0;
  vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);

  // direction light
  vec3 lightDir = normalize(vec3(-3, 3, -2));

  // marching loop
  float dist;
  float depth = 0.0;
  float eps = 0.001;
  vec3 dPos = cPos;
  for(int i = 0; i < 32; i++){
    dist = distScene(dPos);
    depth += dist;
    dPos = cPos + depth * ray;
    if (abs(dist) < eps) break;
  }

  // hit check
  vec3 color;
  if(abs(dist) < eps) {
    vec3 normal = getNormal(dPos);
    float diffuse = clamp(dot(lightDir, normal), 0.1, 1.0);
    color = vec3(1.0, 1.0, 1.0) * diffuse;
  } else {
    color = vec3(0.0);
  }
  gl_FragColor = vec4(color, 1.0);
}
</script><!-- /fragment shader -->

<!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
  attribute vec3 position;

void main(void){
  gl_Position = vec4(position, 1.0);
}
</script><!-- /vertex shader -->

<script src="script.js" type="text/javascript"></script>
<style type="text/css">
  * {
    text-align: center;
    margin: 10px auto;
    padding: 0px;
  }
  canvas {
    box-shadow: 0px 0px 0px 1px gray;
  }
</style>
<title>wgld.org WebGL sample 085</title>
</head>
<body>
  <canvas id="canvas"></canvas>
  <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
  <p><input type="button" value="save image" onclick="saveImage();"></p>
</body></html>
