<!DOCTYPE html>
<!--
Copyright (c) 2015 gam0022
Released under the MIT license
http://opensource.org/licenses/mit-license.php
-->
<html lang="en">
	<head>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<title>three.js webgl - raymarching - reflect</title>
		<style type="text/css">
			body {
				background-color: black;
				margin: 0;
				padding: 0;
			}

			a { color: skyblue }

			canvas {
				display: block;
				position: absolute;
				top: 0;
				left: 0;
				right: 0;
				bottom: 0;
				margin: auto;
			}

			#info {
				color: white;
				font-size: 13px;
				position: absolute;
				bottom: 10px;
				width: 100%;
				text-align: center;
				z-index: 100;
			}
		</style>
	</head>
	<body>

		<div id="info">
			<a href="http://threejs.org" target="_blank">three.js</a> - webgl raymarching example -
			reflect by <a href="https://github.com/gam0022" target="_blank">gam0022</a> (<a href="http://qiita.com/gam0022/items/03699a07e4a4b5f2d41f" target="_blank">article in Japanese</a>)
		</div>

		<script id="fragment_shader" type="x-shader/x-fragment">

#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

uniform vec3 cameraPos;
uniform vec3 cameraDir;

const float EPS = 0.01;
const float OFFSET = EPS * 100.0;
const float PI = 3.14159;

vec3 cPos, cDir, sPos;
vec3 lColor;

const vec3 lightDir = vec3( -0.48666426339228763, 0.8111071056538127, -0.3244428422615251 );

// distance functions
float PrBox2Df (vec2 p, vec2 b) {

  vec2 d = abs( p ) - b;
  return min( max( d.x, d.y ), 0.0 ) + length( max( d, 0.0 ) );

}

float MBoxDf (vec3 p) {
  vec4 q, q0;
  const float mScale = 2.7;
  const int nIter = 12;
  q0 = vec4 (p, 1.);
  q = q0;
  for ( int n = 0; n < nIter; n++ ) {
    q.xyz = clamp( q.xyz, -1.0, 1.0 ) * 2.0 - q.xyz;
    q = q * mScale / clamp( dot( q.xyz, q.xyz ), 0.5, 1.0 ) + q0;
  }
  return length( q.xyz ) / abs( q.w );
}

vec3 onRep( vec3 p, float interval ) {

	return mod( p, interval ) - 0.5 * interval;

}

float sphereDist( vec3 p, vec3 c, float r ) {

	return length( p - c ) - r;

}

vec4 minVec4( vec4 a, vec4 b ) {

	return ( a.a < b.a ) ? a : b;

}

vec3 hsv2rgb( vec3 c ) {

	vec4 K = vec4( 1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0 );
	vec3 p = abs( fract( c.xxx + K.xyz ) * 6.0 - K.www );
	return c.z * mix( K.xxx, clamp( p - K.xxx, 0.0, 1.0 ), c.y );

}

float sceneDist( vec3 p ) {

	return min(
		sphereDist( p, sPos, 0.02 ),
		MBoxDf( onRep( p, 7.0 ) )
	);

}

vec2 circuit( vec2 p ) {

	p = fract(p);
	float r = 0.123;
	float v = 0.0, g = 0.0;
	r = fract(r * 9184.928);
	float cp, d;
	
	d = p.x;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.x - 1.0;
	g += pow(clamp(3.0 - abs(d), 0.0, 1.0), 1000.0);
	d = p.y - 1.0;
	g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 10000.0);
	
	const int iter = 12;
	for(int i = 0; i < iter; i ++)
	{
		cp = 0.5 + (r - 0.5) * 0.9;
		d = p.x - cp;
		g += pow(clamp(1.0 - abs(d), 0.0, 1.0), 200.0);
		if(d > 0.0) {
			r = fract(r * 4829.013);
			p.x = (p.x - cp) / (1.0 - cp);
			v += 1.0;
		}
		else {
			r = fract(r * 1239.528);
			p.x = p.x / cp;
		}
		p = p.yx;
	}
	v /= float(iter);
	return vec2(g, v);

}

vec4 sceneColor( vec3 p ) {

	vec2 uv = p.xz;
	vec2 dg = circuit(uv);

	float glow = max( sin( length(p) - 1.8 * time ) * 2.5, 0.0 );
	if( dg.x < 1.1 ) glow = 0.0;

	return minVec4(
		vec4( 0.5 - 0.5 * cos( time * 0.2 ) * lColor, sphereDist( p, sPos, 0.02 ) ),
		vec4( vec3( 0.2, 0.2, 0.2 ) + lColor * glow, MBoxDf( onRep( p, 7.0 ) ) )
	);

}

vec3 getNormal( vec3 p ) {

	return normalize(vec3(
		sceneDist(p + vec3( EPS, 0.0, 0.0 ) ) - sceneDist(p + vec3( -EPS, 0.0, 0.0 ) ),
		sceneDist(p + vec3( 0.0, EPS, 0.0 ) ) - sceneDist(p + vec3( 0.0, -EPS, 0.0 ) ),
		sceneDist(p + vec3( 0.0, 0.0, EPS ) ) - sceneDist(p + vec3( 0.0, 0.0, -EPS ) )
	));

}

float getShadow( vec3 ro, vec3 rd ) {

	float h = 0.0;
	float c = 0.0;
	float r = 1.0;
	float shadowCoef = 0.5;

	for ( float t = 0.0; t < 50.0; t++ ) {

		h = sceneDist( ro + rd * c );

		if ( h < EPS ) return shadowCoef;

		r = min( r, h * 16.0 / c );
		c += h;

	}

	return 1.0 - shadowCoef + r * shadowCoef;

}

vec3 getRayColor( vec3 origin, vec3 ray, out vec3 p, out vec3 normal, out bool hit ) {

	// marching loop
	float dist;
	float depth = 0.0;
	p = origin;
	int count = 0;

	for ( int i = 0; i < 64; i++ ){

		dist = sceneDist( p );
		depth += dist;
		p = origin + depth * ray;

		count = i;
		if ( abs(dist) < EPS ) break;

	}

	// hit check and calc color
	vec3 color;

	if ( abs(dist) < EPS ) {

		normal = getNormal(p);
		float diffuse = clamp( dot( lightDir, normal ), 0.1, 1.0 );
		float specular = pow( clamp( dot( reflect( lightDir, normal ), ray ), 0.0, 1.0 ), 10.0 );
		float shadow = 0.0;//getShadow( p + normal * OFFSET, lightDir );
		color = ( sceneColor( p ).rgb * diffuse + specular ) * max( 0.5, shadow );

		hit = true;

	} else {

		color = vec3(0.1);

	}

	return color + clamp( cos( time * 0.2 ) * 0.2 * depth - 0.005 * float(count), -1.0, 1.0 );

}

void main(void) {

	// fragment position
	vec2 p = ( gl_FragCoord.xy * 2.0 - resolution ) / min( resolution.x, resolution.y );

	// camera and ray
	if ( true ) {
		cPos  = vec3( -0.8185093402862549, 4.509979248046875, time );
		cDir  = normalize( vec3( sin( time * 0.5 ), sin( time * 0.1 ), cos( time * 0.6 ) + 0.5 ) );
	} else {
		cPos  = cameraPos;
		cDir  = cameraDir;
	}
	vec3 cSide = normalize( cross( cDir, vec3( 1.0, 1.0 ,0.0 ) ) );
	vec3 cUp   = normalize( cross( cSide, cDir ) );
	float targetDepth = 1.3;
	vec3 ray = normalize( cSide * p.x + cUp * p.y + cDir * targetDepth );

	// sphere pos
	float d = 0.2 + 0.1 * cos( time * 0.5 );
	sPos = cPos + vec3( 0.0, 0.0, d );

	// light Color
	lColor = hsv2rgb( vec3( time * 0.02 + 0.6, 1.0, 1.0 ) );

	vec3 color = vec3( 0.0 );
	vec3 q, normal;
	bool hit;
	float alpha = 1.0;

	for ( int i = 0; i < 3; i++ ) {

		color += alpha * getRayColor( cPos, ray, q, normal, hit );
		alpha *= 0.5;
		ray = normalize( reflect( ray, normal ) );
		cPos = q + normal * OFFSET;

		if ( !hit ) break;

	}

	gl_FragColor = vec4(color, 1.0);

}

		</script>

		<script id="vertex_shader" type="x-shader/x-vertex">

			attribute vec3 position;

			void main(void) {

				gl_Position = vec4(position, 1.0);

			}

		</script>

		<script src="js/three.min.js"></script>
		<script src="js/controls/FlyControls.js"></script>
		<script src="js/controls/OrbitControls.js"></script>

		<script src="js/libs/stats.min.js"></script>
		<script src="js/libs/dat.gui.min.js"></script>

		<script>

			var camera, dummyCamera, scene, flyControls, orbitControls, renderer;
			var geometry, material, mesh;
			var mouse = new THREE.Vector2( 0.5, 0.5 );
			var canvas;
			var stats;

			var clock = new THREE.Clock();

			var config = {
				saveImage: function() {

					renderer.render( scene, dummyCamera );
					window.open( canvas.toDataURL() );

				},
				camera: "Orbit",
				resolution: '256',
			};

			init();
			render();

			function init() {

				scene = new THREE.Scene();
				camera = new THREE.PerspectiveCamera(35, 800/600);
				dummyCamera = new THREE.Camera();
				camera.lookAt( new THREE.Vector3( 0.0, 0.0, 0.0 ) );

				geometry = new THREE.PlaneBufferGeometry( 2.0, 2.0 );
				material = new THREE.RawShaderMaterial( {
					uniforms: {
						resolution: { type: 'v2', value: new THREE.Vector2( config.resolution, config.resolution ) },
						mouse: { type: 'v2', value: mouse },
						time: { type: 'f', value: 0.0 },
						cameraPos: { type: 'v3', value: camera.getWorldPosition() },
						cameraDir: { type: 'v3', value: camera.getWorldDirection() },
					},
					vertexShader: document.getElementById( 'vertex_shader' ).textContent,
					fragmentShader: document.getElementById( 'fragment_shader' ).textContent
				} );
				mesh = new THREE.Mesh( geometry, material );
				scene.add( mesh );

				renderer = new THREE.WebGLRenderer();
				//renderer.setPixelRatio( window.devicePixelRatio );
				renderer.setSize( config.resolution, config.resolution );

				canvas = renderer.domElement;
				//canvas.addEventListener( 'mousemove', onMouseMove );
				window.addEventListener( 'resize', onWindowResize );
				document.body.appendChild( canvas );

				//flyControls = new THREE.FlyControls( camera, canvas );
				//flyControls.autoForward = true;
				//flyControls.dragToLook = false;
				//flyControls.rollSpeed = Math.PI / 12;
				//flyControls.movementSpeed = 0.1;

				orbitControls = new THREE.OrbitControls( camera, canvas );
				orbitControls.enableDamping = false;
				orbitControls.dampingFactor = 0.015;
				orbitControls.enableZoom = true;
				orbitControls.zoomSpeed = 0.1;
				orbitControls.rotateSpeed = 0.8;
				orbitControls.autoRotate = false;
				orbitControls.autoRotateSpeed = 0.0;
				orbitControls.target = new THREE.Vector3( 0.0, 0.0, 2.0 );

				var gui = new dat.GUI();
				gui.add( config, 'saveImage' ).name( 'Save Image' );
				gui.add( config, 'camera', [ 'Orbit', 'Fly' ] ).name( 'Camera' );
				gui.add( config, 'resolution', [ '256', '512', '800', 'full' ] ).name( 'Resolution' ).onChange( function( value ) {

					if ( value !== 'full' ) {

						canvas.width = value;
						canvas.height = value;

					}
					onWindowResize();

				} );

				stats = new Stats();
				document.body.appendChild( stats.domElement );

			}

			function render( timestamp ) {

				var delta = clock.getDelta();

				stats.begin();

				if ( config.camera === "Fly" ) {

					flyControls.update( delta );

				} else if ( config.camera === "Orbit" ) {

					orbitControls.update();

				} else {

					camera.position.set( mouse.x - 0.5, mouse.y * 4, timestamp );
					camera.lookAt( new THREE.Vector3( 0.0, -0.3, 1.0 ).add( camera.position ) );

				}

				if ( camera.position.y < 0 ) camera.position.y = 0;

				material.uniforms.resolution.value = new THREE.Vector2( canvas.width, canvas.height );
				material.uniforms.mouse.value = mouse;
				material.uniforms.time.value = timestamp * 0.001;
				material.uniforms.cameraPos.value = camera.getWorldPosition();
				material.uniforms.cameraDir.value = camera.getWorldDirection();
				renderer.render( scene, dummyCamera );

				stats.end();
				requestAnimationFrame( render );

				var pos = camera.getWorldPosition();
				var dir = camera.getWorldDirection();

				document.getElementById( 'info' ).innerHTML = ""
					+ pos.x + ", " + pos.y + ", " + pos.z + ". "
					+ dir.x + ", " + dir.y + ", " + dir.z + ". ";

			}

			function onMouseMove( e ) {

				mouse.x = e.offsetX / canvas.width;
				mouse.y = e.offsetY / canvas.height;

			}

			function onWindowResize( e ) {

				if ( config.resolution === 'full' ) {

					canvas.width = window.innerWidth;
					canvas.height = window.innerHeight;

				}

				renderer.setSize( canvas.width, canvas.height );

			}

		</script>

	</body>
</html>
