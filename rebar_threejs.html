<html>
  <head><title>Raymarching Sample</title></head>
  <body>
<script id="fragment_shader" type="x-shader/x-fragment">
precision highp float;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

vec2 onRep(vec2 p, float interval) {
  return mod(p, interval) - interval * 0.5;
}

float distBar(vec2 p, float interval, float width) {
  return length(max(abs(onRep(p, interval)) - width, 0.0));
}

float distTube(vec2 p, float interval, float width) {
  return length(onRep(p, interval)) - width;
}

// distance function
float distScene(vec3 p) {
  float bar_x = distBar(p.yz, 1.0, 0.1);
  float bar_y = distBar(p.xz, 1.0, 0.1);
  float bar_z = distBar(p.xy, 1.0, 0.1);

  float tube_x = distTube(p.yz, 0.1, 0.025);
  float tube_y = distTube(p.xz, 0.1, 0.025);
  float tube_z = distTube(p.xy, 0.1, 0.025);

  return max(max(max(min(min(bar_x, bar_y),bar_z), -tube_x), -tube_y), -tube_z);
}

vec3 getNormal(vec3 p) {
  float d = 0.0001;
  return normalize(vec3(
    distScene(p + vec3(  d, 0.0, 0.0)) - distScene(p + vec3( -d, 0.0, 0.0)),
    distScene(p + vec3(0.0,   d, 0.0)) - distScene(p + vec3(0.0,  -d, 0.0)),
    distScene(p + vec3(0.0, 0.0,   d)) - distScene(p + vec3(0.0, 0.0,  -d))
  ));
}

void main(void) {
  // fragment position
  vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

  // camera and ray
  vec3 cPos = vec3(0.0, 0.0, time * 0.001);
  vec3 cDir = normalize(vec3(0.1, 0.1, 1.0));
  vec3 cUp  = normalize(vec3(0.1, 0.4, 0.0));
  vec3 cSide = cross(cDir, cUp);
  float targetDepth = 1.0;
  vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);

  // direction light
  vec3 lightDir = normalize(vec3(1, 1, -2));

  // marching loop
  float dist;
  float depth = 0.0;
  float eps = 0.001;
  vec3 dPos = cPos;
  for(int i = 0; i < 64; i++){
    dist = distScene(dPos);
    depth += dist;
    dPos = cPos + depth * ray;
    if (abs(dist) < eps) break;
  }

  // hit check
  vec3 color;
  if(abs(dist) < eps) {
    vec3 normal = getNormal(dPos);
    float diffuse = clamp(dot(lightDir, normal), 0.1, 1.0);
    color = vec3(1.0, 0.1, 0.1) * diffuse;
  } else {
    color = vec3(0.0);
  }
  gl_FragColor = vec4(color + 0.05 * depth, 1.0);
}
</script>

<script id="vertex_shader" type="x-shader/x-vertex">
void main(void) {
  gl_Position = vec4(position, 1.0);
}
</script>

<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r73/three.min.js" type="text/javascript"></script>
<script type="text/javascript">
var scene, camera, renderer;
var geometry, material, mesh;

init();
animate();

function init() {
  scene = new THREE.Scene();
  camera = new THREE.Camera();

  geometry = new THREE.PlaneBufferGeometry(2.0, 2.0);
  material = new THREE.ShaderMaterial({
    uniforms: {
      time: { type: "f" },
      resolution: { type: "v2" }
    },
    vertexShader: document.getElementById('vertex_shader').textContent,
    fragmentShader: document.getElementById('fragment_shader').textContent
  });
  mesh = new THREE.Mesh(geometry, material);
  scene.add(mesh);

  renderer = new THREE.WebGLRenderer();
  renderer.setSize(512.0, 512.0);

  document.body.appendChild(renderer.domElement);
}

function animate(dt) {
  requestAnimationFrame(animate);

  material.uniforms.time.value = dt;
  material.uniforms.resolution.value = new THREE.Vector2(512.0, 512.0);

  renderer.render(scene, camera);
}
</script>
</body>
</html>
