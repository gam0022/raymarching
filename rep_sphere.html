<html><head><!-- fragment shader --><script id="fs" type="x-shader/x-fragment">precision mediump float;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

vec3 onRep(vec3 p, vec3 c) {
  return mod(p, c) - 0.5 * c;
}

vec2 onRep(vec2 p, float c) {
  return mod(p, c) - 0.5 * c;
}

float displacement(vec3 p) {
  float a = 0.05;
  float b = 10.0;
  float c = time * 5.0;
  return a * sin(c + b * p.x) * sin(c + b * p.y) * sin(c + b * p.z);
}

float udRoundBox( vec3 p, vec3 b, float r )
{
  return length(max(abs(p)-b,0.0))-r;
}

// box distance function
float distFuncBox(vec3 p){
  float a = mod(0.02 * time, 0.2) + 0.01;
  return length(max(abs(p) - vec3(a, a, a), 0.0)) - 0.05;
}

// torus distance function
float distFuncTorus(vec3 p){
  vec2 t = vec2(1.5, 0.25);
  vec2 r = vec2(length(p.xy) - t.x, p.z);
  return length(r) - t.y;
}

float distFuncSphere(vec3 p) {
  return length(p) - 0.3;
}

// distance function
float distFunc(vec3 p){
  vec3 c = vec3(1.0, 1.0, 1.0);
  float d2 = distFuncSphere(onRep(p,c));
  return d2;
  //return min(d1, d2);  // 同時に描く
  //return max(d1, d2);  // 重なっている部分だけを描く
  //return max(-d1, d2); // d1が重なっていないd2部分を描く
  //return max(d1, -d2); // d2が重なっていないd1部分を描く
}

vec3 genNormal(vec3 p){
  float d = 0.0001;
  return normalize(vec3(
    distFunc(p + vec3(  d, 0.0, 0.0)) - distFunc(p + vec3( -d, 0.0, 0.0)),
    distFunc(p + vec3(0.0,   d, 0.0)) - distFunc(p + vec3(0.0,  -d, 0.0)),
    distFunc(p + vec3(0.0, 0.0,   d)) - distFunc(p + vec3(0.0, 0.0,  -d))
  ));
}

void main(void){
  // fragment position
  vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);

  // camera and ray
  vec3 cPos = vec3(0.0,  0.0,  0.0 + time);
  vec3 cDir = normalize(vec3(0.1, 0.1, 1.0));
  vec3 cUp  = normalize(vec3(0.1, 0.4, 0.0));
  vec3 cSide = cross(cDir, cUp);
  float targetDepth = 1.0;
  vec3 ray = normalize(cSide * p.x + cUp * p.y + cDir * targetDepth);

  vec3 lightDir = normalize(vec3(1, 1, -2));

  // marching loop
  float tmp, dist;
  tmp = 0.0;
  vec3 dPos = cPos;
  for(int i = 0; i < 64; i++){
    dist = distFunc(dPos);
    tmp += dist;
    dPos = cPos + tmp * ray;
    if (abs(dist) <= 0.001) break;
  }

  // hit check
  vec3 color;
  if(abs(dist) < 0.001){
    vec3 normal = genNormal(dPos);
    float diff = clamp(dot(lightDir, normal), 0.1, 1.0);
    color = vec3(1.0, 1.0, 1.0) * diff;
  }else{
    color = vec3(0.0);
  }
  gl_FragColor = vec4(color, 1.0);
}
</script><!-- /fragment shader -->

<!-- vertex shader --><script id="vs" type="x-shader/x-vertex">
  attribute vec3 position;

void main(void){
  gl_Position = vec4(position, 1.0);
}
</script><!-- /vertex shader -->

<script src="script.js" type="text/javascript"></script>
<style type="text/css">
  * {
    text-align: center;
    margin: 10px auto;
    padding: 0px;
  }
  canvas {
    box-shadow: 0px 0px 0px 1px gray;
  }
</style>
<title>wgld.org WebGL sample 085</title>
</head>
<body>
  <canvas id="canvas"></canvas>
  <p><input type="checkbox" id="check" checked><label for="check"> auto run</label></p>
</body></html>
